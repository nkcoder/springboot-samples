buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/central" }
    }
    dependencies {
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.9.1"
        classpath "org.owasp:dependency-check-gradle:5.0.0-M2"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.7.1"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
    }
}

plugins {
    id 'application'
    id "org.springframework.boot"
    id "io.spring.dependency-management"
}

repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
}

application {
    mainClassName = "com.ecommerce.order.Application"
    applicationDefaultJvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    //spring dependencies
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-jdbc")

    //common dependencies
    implementation('io.jsonwebtoken:jjwt:0.9.1')
    implementation("org.flywaydb:flyway-core")
    implementation('com.google.guava:guava:27.0.1-jre')
    implementation('commons-io:commons-io:2.6')
    implementation('org.apache.commons:commons-lang3')
    implementation('org.apache.commons:commons-collections4:4.2')
    implementation('io.springfox:springfox-swagger2:2.9.2')
    implementation('io.springfox:springfox-swagger-ui:2.9.2')
    implementation('com.cwbase:logback-redis-appender:1.1.5')
    implementation('net.javacrumbs.shedlock:shedlock-spring:2.4.0')
    implementation('net.javacrumbs.shedlock:shedlock-provider-jdbc-template:2.4.0')


    //jackson dependencies
    implementation('com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.8')
    

    //test dependencies
    testImplementation("io.rest-assured:spring-mock-mvc")
}


sourceSets {
    componentTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }

    apiTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}


configurations {
    componentTestImplementation.extendsFrom testImplementation
    componentTestRuntimeOnly.extendsFrom testRuntimeOnly
    apiTestImplementation.extendsFrom testImplementation
    apiTestRuntimeOnly.extendsFrom testRuntimeOnly
}


task componentTest(type: Test) {
    description = 'Run component tests.'
    group = 'verification'
    testClassesDirs = sourceSets.componentTest.output.classesDirs
    classpath = sourceSets.componentTest.runtimeClasspath
    shouldRunAfter test
}


task apiTest(type: Test) {
    description = 'Run API tests.'
    group = 'verification'
    testClassesDirs = sourceSets.apiTest.output.classesDirs
    classpath = sourceSets.apiTest.runtimeClasspath
    shouldRunAfter componentTest
}

configurations.all {
    exclude group: "junit", module: "junit"
    exclude group: "org.assertj", module: "assertj-core"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

check.dependsOn componentTest
check.dependsOn apiTest

bootJar {
    launchScript()
}

apply from: "${projectDir}/config/idea.gradle"
apply from: "${projectDir}/config/docker-compose/docker-compose.gradle"
apply from: "${projectDir}/config/dependency-check/dependency-check.gradle"
apply from: "${projectDir}/config/spotbugs/spotbugs.gradle"
apply from: "${projectDir}/config/checkstyle/checkstyle.gradle"
apply from: "${projectDir}/config/git-hooks/git-hooks.gradle"
apply from: "${projectDir}/config/version-info.gradle"
apply from: "${projectDir}/config/jacoco.gradle"
//apply from: "${rootProject.projectDir}/gradle/sonarqube.gradle"
